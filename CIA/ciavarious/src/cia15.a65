
tmp=$fc
addr=$fd
add2=$f9

TMP=$6000
DATA=$8000

#define	TESTLEN $40

	.word $0801
	*=$0801

	.word link, 10
	.byt $9e, "2080", 0
link	.word 0
	.dsb 2080-*, 0

	jsr $e518
loop	jsr $ffe4	
	cmp #"@"
	bne noende
	rts
noende
	cmp #"A"
	bcc loop
	cmp #"C"+1
	bcs loop
	sec
	sbc #"A"
	sta tmp
	jsr $e518

	lda #0
	sta addr+1
	lda tmp
	asl
	rol addr+1
	asl
	rol addr+1
	asl
	rol addr+1
	asl
	rol addr+1
	asl
	rol addr+1
	asl
	rol addr+1
	sta addr
	lda #$0a
	clc
	adc addr+1
	sta addr+1

	sei
	lda #$0b
	sta $d011

l0	lda $d012
	bne l0
l2	lda $d012
	beq l2
l3	lda $d012
	bne l3

	lda #0
	sta $dc0e
	sta $dc0f
	lda #$7f
	sta $dc0d
	lda $dc0d
	lda $dc0d
	ldy #0
	tya
t1a	sta $dc04
	sta $dc05
	sta $dc06
	sta $dc07
	dey
	bne t1a

	lda $dc0e
	ora #$80
	sta $dc0e
	lda $dc0f
	and #$7f
	sta $dc0f
	lda $dc08

	jsr dojump

	lda #$1b
	sta $d011
	jsr $fda3
	cli

	lda tmp
	clc
	adc #>TMP
	sta addr+1
	lda #<TMP
	sta addr
	lda tmp
	clc
	adc #>DATA
	sta add2+1
	lda #<DATA
	sta add2
	ldy #0
ll	lda (addr),y
	sta $0400,y
	cmp (add2),y
	bne rot
	lda #5
	.byt $2c
rot	lda #2
	sta $d800,y
	iny
	bne ll

	jmp loop

dojump	jmp (addr)

	/* wait for TOD .1 sec change */
waitchange .(
	cmp $dc08
	beq waitchange
	rts
	.)

	.dsb $0a00-*,0

;------------------------------------------
	.(		; A
test
	ldx #0
t1b	stx $dc0b
	lda #$59
	sta $dc0a
	sta $dc09
	lda #$08
	sta $dc08
	jsr waitchange
	lda $dc0b
	sta TMP,x
	inx
	bne t1b
	rts
	.dsb test+TESTLEN-*,0
-TMP 	+=$100
	.)

	.(		; B
test
	ldx #0
t1b	stx $dc0b
	lda #$59
	sta $dc0a
	sta $dc09
	lda #$08
	sta $dc08
	jsr waitchange
	lda #$09
	jsr waitchange
	lda $dc0b
	sta TMP,x
	inx
	bne t1b
	rts
	.dsb test+TESTLEN-*,0
-TMP 	+=$100
	.)

	.(		; C
test
	ldx #0
t1b	stx $dc0b
	lda #$59
	sta $dc0a
	sta $dc09
	lda #$08
	sta $dc08
	jsr waitchange
	lda #$09
	jsr waitchange
	lda #$00
	jsr waitchange
	lda $dc0b
	sta TMP,x
	inx
	bne t1b
	rts
	.dsb test+TESTLEN-*,0
-TMP 	+=$100
	.)


